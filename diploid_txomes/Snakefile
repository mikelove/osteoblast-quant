configfile: "config.json"

SALMON = "/proj/milovelab/bin/salmon-1.5.2_linux_x86_64/bin/salmon"

rule all:
    input:
        expand("indices/{strain}/pos.bin", strain=config["strain"])

rule combine_vcf:
    input:
        snp = "snps/{file}.mgp.v5.snps.dbSNP142.vcf.gz",
        indel = "snps/{file}.mgp.v5.indels.dbSNP142.normed.vcf.gz"
    params: 
        unsort = "snps/{file}.unsort.vcf",
        unzip = "snps/{file}.merged.vcf"
    output: "snps/{file}.merged.vcf.gz"
    shell: 
        """
        gunzip -c {input.snp} > {params.unsort}
        zcat {input.indel} | sed /^#/d >> {params.unsort}
        bcftools sort -m 3G {params.unsort} > {params.unzip}
        rm -f {params.unsort}
        bgzip {params.unzip}
        tabix -p vcf {output}
        """

rule vcf2vci:
    input: 
        vcf = "snps/{file}.merged.vcf.gz",
        fasta = "fasta/Mus_musculus.GRCm38.dna.primary_assembly_bgzip.fa.gz"
    output: "snps/{file}.vci.gz"
    params: 
        vci = "snps/{file}.vci"
    shell: 
        "g2gtools vcf2vci -p 12 -i {input.vcf} -o {params.vci} "
        "-s {wildcards.file} -f {input.fasta}"

rule patch:
    input:
        vci = "snps/{file}.vci.gz",
        fasta = "fasta/Mus_musculus.GRCm38.dna.primary_assembly_bgzip.fa.gz"
    output: "fasta/{file}.patched.fa"
    shell:
        "g2gtools patch -p 12 -i {input.fasta} -c {input.vci} -o {output}"

rule transform:
    input:
        vci = "snps/{file}.vci.gz",
        fasta = "fasta/{file}.patched.fa"
    output: "fasta/{file}.trans.fa"
    shell:
        "g2gtools transform -p 12 -i {input.fasta} -c {input.vci} -o {output}"

rule convert:
    input:
        vci = "snps/{file}.vci.gz",
        gtf = "genes/Mus_musculus.GRCm38.102.gtf",
    output: "genes/{file}.gtf"
    shell:
        "g2gtools convert -i {input.gtf} -c {input.vci} -o {output}"

rule db:
    input: "genes/{file}.gtf"
    output: "genes/{file}.db"
    shell:
        "g2gtools gtf2db -i {input} -o {output}"

rule extract_txps:
    input:
        fasta = "fasta/{file}.trans.fa",
        db = "genes/{file}.db"
    output: "txps/{file}.transcripts.fa"
    shell:
        "g2gtools extract -i {input.fasta} -db {input.db} --transcripts > {output}"

rule tag_ref_and_strain:
    input: expand("txps/{strain}.transcripts.fa", strain=config["strain"])
    output: 
        ref = "txps/Mus_musculus.GRCm38.102.subset.fa",
        strains = expand("txps/{strain}.tagged.fa", strain=config["strain"])
    params:
        ref = "txps/Mus_musculus.GRCm38.102.fa"
    shell:
        "R CMD BATCH --no-save --no-restore '--args {input} {params.ref}' rearrange_ensembl.R"

rule make_diploid_fasta:
    input:
        ref = "txps/Mus_musculus.GRCm38.102.subset.fa",
        strain = "{file}.tagged.fa"
    output: "{file}.diploid.fa"
    shell:
        "cat {input.ref} {input.strain} > {output}"

rule index_diploid_fasta:
    input: "txps/{file}.diploid.fa"
    output: "indices/{file}/pos.bin"
    params:
        dir = "indices/{file}"
    shell: "{SALMON} index -p 12 -t {input} -i {params.dir} --keepDuplicates"
